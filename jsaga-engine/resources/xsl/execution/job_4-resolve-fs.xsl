<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
            xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
            xmlns="http://schemas.ggf.org/jsdl/2005/11/jsdl"
            xmlns:jsdl="http://schemas.ggf.org/jsdl/2005/11/jsdl"
            xmlns:posix="http://schemas.ggf.org/jsdl/2005/11/jsdl-posix"
            xmlns:ext="http://www.in2p3.fr/jsdl-extension"
            exclude-result-prefixes="jsdl">
    <!-- ###########################################################################
         # Simplify job description
         ###########################################################################
    -->
    <xsl:output method="xml" indent="yes"/>

    <!-- entry point -->
    <xsl:template match="/">
        <xsl:apply-templates select="ext:Job"/>
    </xsl:template>
    <xsl:template match="ext:Job">
        <ext:Job>
            <xsl:comment> GENERATED BY job_4-resolve-fs.xsl </xsl:comment>
            <xsl:apply-templates select="@* | * | text() | comment()"/>
        </ext:Job>
    </xsl:template>
    <xsl:template match="jsdl:JobDefinition">
        <JobDefinition>
            <xsl:apply-templates/>
        </JobDefinition>
    </xsl:template>

    <xsl:template match="posix:WorkingDirectory">
        <posix:WorkingDirectory>
            <xsl:if test="@filesystemName and not(starts-with(text(), '/'))">
                <xsl:variable name="fsName" select="@filesystemName"/>
                <xsl:variable name="mntPoint" select="//jsdl:FileSystem[@name=$fsName]/jsdl:MountPoint/text()"/>
                <xsl:if test="not($mntPoint)">
                    <xsl:message terminate="yes">File system not defined: <xsl:value-of select="$fsName"/></xsl:message>
                </xsl:if>
                <xsl:value-of select="$mntPoint"/>
                <xsl:text>/</xsl:text>
            </xsl:if>
            <xsl:value-of select="text()"/>
        </posix:WorkingDirectory>
    </xsl:template>

    <xsl:template match="jsdl:DataStaging">
        <DataStaging>
            <xsl:copy-of select="@*"/>
            <xsl:choose>
                <xsl:when test="jsdl:FilesystemName/text() and not(starts-with(jsdl:FileName/text(), '/'))">
                    <xsl:variable name="fsName" select="jsdl:FilesystemName/text()"/>
                    <xsl:variable name="mntPoint" select="//jsdl:FileSystem[@name=$fsName]/jsdl:MountPoint/text()"/>
                    <xsl:if test="not($mntPoint)">
                        <xsl:message terminate="yes">File system not defined: <xsl:value-of select="$fsName"/></xsl:message>
                    </xsl:if>
                    <FileName>
                        <xsl:value-of select="$mntPoint"/>/<xsl:value-of select="jsdl:FileName/text()"/>
                    </FileName>
                    <FilesystemName>
                        <xsl:value-of select="$mntPoint"/>
                    </FilesystemName>
                </xsl:when>
                <xsl:otherwise>
                    <FileName>
                        <xsl:value-of select="jsdl:FileName/text()"/>
                    </FileName>
                    <FilesystemName>
                        <xsl:text>$HOME</xsl:text>
                    </FilesystemName>
                </xsl:otherwise>
            </xsl:choose>
            <xsl:apply-templates select="*[local-name()!='FileName' and local-name()!='FilesystemName']"/>
        </DataStaging>
    </xsl:template>

    <!-- default template rules -->
    <xsl:template match="*">
        <xsl:element name="{name()}">
            <xsl:copy-of select="@*"/>
            <xsl:apply-templates/>
        </xsl:element>
    </xsl:template>
    <xsl:template match="comment()">
        <xsl:comment><xsl:value-of select="."/></xsl:comment>
    </xsl:template>
</xsl:stylesheet>
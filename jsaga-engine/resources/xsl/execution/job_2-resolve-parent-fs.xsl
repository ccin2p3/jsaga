<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
            xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
            xmlns="http://schemas.ggf.org/jsdl/2005/11/jsdl"
            xmlns:jsdl="http://schemas.ggf.org/jsdl/2005/11/jsdl"
            xmlns:posix="http://schemas.ggf.org/jsdl/2005/11/jsdl-posix"
            xmlns:ext="http://www.in2p3.fr/jsdl-extension"
            exclude-result-prefixes="jsdl">
    <!-- ###########################################################################
         # Resolve parent file systems
         ###########################################################################
    -->
    <xsl:output method="xml" indent="yes"/>

    <!-- entry point -->
    <xsl:template match="/">
        <xsl:apply-templates select="ext:Job"/>
    </xsl:template>
    <xsl:template match="ext:Job">
        <ext:Job>
            <xsl:comment> GENERATED BY job_2-resolve-parent-fs.xsl </xsl:comment>
            <xsl:choose>
                <xsl:when test="jsdl:JobDefinition/jsdl:JobDescription/jsdl:Resources/jsdl:FileSystem/ext:ParentFilesystemName">
                    <xsl:apply-templates select="@* | * | text() | comment()"/>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:copy-of select="@* | * | text() | comment()"/>
                </xsl:otherwise>
            </xsl:choose>
        </ext:Job>
    </xsl:template>

    <!-- workaround to namespace problems... -->
    <xsl:template match="jsdl:JobDefinition">
        <JobDefinition>
            <xsl:apply-templates/>
        </JobDefinition>
    </xsl:template>

    <!-- specific templates here -->
    <xsl:template match="jsdl:FileSystem[ext:ParentFilesystemName]">
        <xsl:variable name="parentName" select="ext:ParentFilesystemName/text()"/>
        <xsl:variable name="parent" select="../jsdl:FileSystem[@name=$parentName]"/>
        <xsl:if test="$parent">
            <FileSystem name="{@name}">
                <xsl:copy-of select="$parent/jsdl:FileSystemType | $parent/jsdl:Description"/>
                <MountPoint>
                    <xsl:value-of select="$parent/jsdl:MountPoint/text()"/>/<xsl:value-of select="jsdl:MountPoint/text()"/>
                </MountPoint>
                <xsl:copy-of select="jsdl:DiskSpace"/>
            </FileSystem>
        </xsl:if>
    </xsl:template>

    <!-- default template rules -->
    <xsl:template match="*">
        <xsl:element name="{name()}">
            <xsl:copy-of select="@*"/>
            <xsl:apply-templates/>
        </xsl:element>
    </xsl:template>
    <xsl:template match="comment()">
        <xsl:comment><xsl:value-of select="."/></xsl:comment>
    </xsl:template>
</xsl:stylesheet>